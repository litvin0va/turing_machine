; 1 0
-; First row is always input tape!!!!

0 * * l add_left_stop
0 _ _ * halt

add_left_stop _ l r skip_first_val
add_left_stop * * * halt ; error

skip_first_val * * r skip_first_val
skip_first_val _ _ * add_middle_stop

add_middle_stop _ m * add_right_stop

add_right_stop * * r add_right_stop
add_right_stop _ r l go_to_start

go_to_start * * l go_to_start
go_to_start l l r get_first

get_first _ _ r get_first
get_first m m r first_is_empty
get_first 1 _ r first_is_1_skip_first_val
get_first 0 _ r first_is_0_skip_first_val

first_is_empty _ _ r first_is_empty
first_is_empty r r l equal
first_is_empty * * l second_won

first_is_1_skip_first_val * * r first_is_1_skip_first_val
first_is_1_skip_first_val m m r first_is_1_check_second

first_is_0_skip_first_val * * r first_is_0_skip_first_val
first_is_0_skip_first_val m m r first_is_0_check_second

first_is_0_check_second _ _ r first_is_0_check_second
first_is_0_check_second 0 _ l go_to_start
first_is_0_check_second 1 _ * second_won_check_sum
first_is_0_check_second r r l first_won

first_is_1_check_second _ _ r first_is_1_check_second
first_is_1_check_second 1 _ l go_to_start
first_is_1_check_second 0 _ * first_won_check_sum
first_is_1_check_second r r l first_won

second_won_check_sum * * * second_won_go_to_start

second_won_go_to_start * * l second_won_go_to_start
second_won_go_to_start l l r second_won_get_first

second_won_get_first _ _ r second_won_get_first
second_won_get_first m m * second_won ; first is empty
second_won_get_first * * r second_won_go_to_second

second_won_go_to_second * * r second_won_go_to_second
second_won_go_to_second m m r second_won_get_second

second_won_get_second _ _ r second_won_get_second
second_won_get_second r r l first_won ; second is empty
second_won_get_second * _ l second_won_check_sum


first_won_check_sum * * * first_won_go_to_start

first_won_go_to_start * * l first_won_go_to_start
first_won_go_to_start l l r first_won_get_first

first_won_get_first _ _ r first_won_get_first
first_won_get_first m m r first_won_first_is_empty
first_won_get_first * _ r first_won_go_to_second

first_won_go_to_second * * r first_won_go_to_second
first_won_go_to_second m m r first_won_get_second

first_won_get_second _ _ r first_won_get_second
first_won_get_second r r l first_won ; second empty
first_won_get_second * _ l first_won_check_sum

first_won_first_is_empty _ _ r first_won_first_is_empty
first_won_first_is_empty r r l first_won
first_won_first_is_empty * * l second_won


first_won * > l clear_right
second_won * < l clear_right
equal * = l clear_right

clear_right * _ r clear_right
clear_right > > r clear_right
clear_right < < r clear_right
clear_right = = r clear_right
clear_right r _ l clear_left

clear_left * _ l clear_left
clear_left > > l clear_left
clear_left < < l clear_left
clear_left = = l clear_left
clear_left l _ * halt
